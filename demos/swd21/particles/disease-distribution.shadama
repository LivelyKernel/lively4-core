program "DiseaseDistribution"

breed Human (x, y, dx, dy, r, g, b, a, LastInfectionTime, Immunity, RandomInfection)
// Helper to access all fields
breed AllFields (x, y)
patch Field (r, g, b, a) // a := pathogen concentration
  
// Ideas: add support for constants or adjustable variables
// Ideas: simplify access to all fields
  
static setup() {
  // Place Humans on random position with random momentum  
  
  Human.fillRandom("RandomInfection", 0, 1);
  Human.fillRandom("x", 0, width);
  Human.fillRandom("y", 0, height);
  Human.fillRandom("Immunity", 0.0, 0.2);
  Human.fillRandomDir("dx", "dy");
  Human.initHuman(time);
  
  // Initialize and reset all fields
  AllFields.fillSpace("x", "y", width, height);
  AllFields.initField(Field);

  // Reset and start loop
  Display.clear();
}

static loop() {
  if (ceil(time) % 5 == 0){
      // Human.fillRandomDir("dx", "dy");
  }
  
  Human.move();
  Human.live(Field, time);
  Human.spread(Field);
  
  //Field.diffuse("b");
  //Field.diffuse("a");
    
  //AllFields.decay(Field);
  
  Human.draw();
  Field.draw();
}

static stop(){
  loop.stop();
  Human.setCount(0);
  loop.step();
  Display.clear();
}

static start(){
  Human.setCount(100);

  setup.step();
  loop.start();
}

static double(){
  Human.setCount(200);
  setup.step();
}

static run(){
  Human.initHuman(time);
  AllFields.initField(Field);
}

def resetHuman(){
  this.LastInfectionTime = 10;
}

def initHuman(time) {
  var rand = this.RandomInfection;
  if (rand < 0.2){
    // Init infected human
    this.LastInfectionTime = time;
    
  }
  else{
    // Init healthy human
    this.LastInfectionTime = -1.0;
  }
}
  

def initField(field){
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = 0.0;
}


def move() {
  
  var dx = this.dx;
  var dy = this.dy;

  var newX = this.x + dx;
  var newY = this.y + dy;

  if (newX < 0.0) {
    newX = -newX;
    dx = -dx;
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x);
    dx = -dx;
  }
  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y);
    dy = -dy;
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y);
    dy = -dy;
  }

  this.x = newX;
  this.y = newY;
  this.dx = dx;
  this.dy = dy;
}

def live(field, time){
  var lastInfectionTime = this.LastInfectionTime;
  var immunity = this.Immunity;
  var pathogenConc = field.a;
    
  // Infect
   if (lastInfectionTime < 0){
    // Human is not infected
    if (pathogenConc > immunity){
      // The human becomes infected
      lastInfectionTime = time;
    }
  }
  
  // Heal
  if (lastInfectionTime > 0){
    var timeDiff = abs(time - lastInfectionTime);
		
    // Heal human after threshold
    var healThreshold = 20;
    if (timeDiff > healThreshold){
      lastInfectionTime = -1.0;
      immunity = 1.0;
    }
  }
  
  
  // Update
  this.LastInfectionTime = lastInfectionTime;
  this.Immunity = immunity;
  if (lastInfectionTime > 0.0){
    // An infected human is red
    this.r = 1.0;
    this.g = 0.0;
    this.b = 0.0;
    this.a = 1.0;
  }
  else{
    if (immunity >= 1.0){
      // An immune human is dark green
      this.r = 0.0;
      this.g = 0.7;
      this.b = 0.0;
      this.a = 1.0;
    }
    else{
      // A healthy human is green
      this.r = 0.0;
      this.g = 1.0;
      this.b = 0.0;
      this.a = 1.0;
    }
  }
}
  

def spread(field) {
  var pathogenConc = field.a;
  
  if (this.LastInfectionTime > 0){
    var spreadFactor = 0.5;
    pathogenConc = pathogenConc + spreadFactor;
  }
  
  if (pathogenConc > 1.0){
    pathogenConc = 1.0;
  }
  
  // Possible bug: One must set these
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = pathogenConc;
}

def decay(field) {
  var decayFactor = 0.0005;
  var pathogenConc = field.a - decayFactor;
  
  if (pathogenConc < 0.0){
    pathogenConc = 0.0;
  }
  
  // Possible bug: One must set these
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = pathogenConc;
}



