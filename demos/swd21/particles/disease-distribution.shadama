program "DiseaseDistribution"

breed Human (x, y, dx, dy, r, g, b, a, LastInfectionTime, Immunity)
// Helper to access all fields
breed AllFields (x, y)
patch Field (r, g, b, a) // a := pathogen concentration
  
// Ideas: add support for constants or adjustable variables
// Ideas: simplify access to all fields
  
static setup() {
  // Place Humans on random position with random momentum
  Human.setCount(100);
  
  Human.fillRandom("x", 0, width);
  Human.fillRandom("y", 0, height);
  Human.fillRandom("Immunity", 0.0, 0.2);
  Human.fillRandomDir("dx", "dy");
  Human.initHuman(time);
  
  // Initialize and reset all fields
  AllFields.fillSpace("x", "y", width, height);
  AllFields.initField(Field);

  // Reset and start loop
  Display.clear();
  loop.start();
}

static loop() {
  if (ceil(time) % 5 == 0){
      // Human.fillRandomDir("dx", "dy");
  }
  
  Human.move();
  Human.spreadDisease(Field);
  
  //Field.diffuse("b");
  //Field.diffuse("a");
  
  Human.infect(Field, time);
  Human.heal(time);
  
  AllFields.decay(Field);
  
  // Human.updateHuman();
  Human.draw();
  
  Field.draw();
}

def initHuman(time) {
  var rand = random(this.x * 1.31 + this.y * 0.2 - time);
  if (rand < 0.2){
    // Init infected human
    this.r = 1.0;
    this.g = 0.0;
    this.b = 0.0;
    this.a = 1.0;
    this.LastInfectionTime = time;
    
  }
  else{
    // Init healthy human
    this.r = 0.0;
    this.g = 1.0;
    this.b = 0.0;
    this.a = 1.0;
    this.LastInfectionTime = -1.0;
  }
}
  
// Not used due to bug / concurrency issues?
def updateHuman() {
   if (this.LastInfectionTime > 0.0){
    // An infected human is red
    this.r = 1.0;
    this.g = 0.0;
    this.b = 0.0;
    this.a = 1.0;
  }
  else{
    if (this.Immunity >= 1.0){
      // An immune human is dark green
      this.r = 0.0;
      this.g = 0.7;
      this.b = 0.0;
      this.a = 1.0;
    }
    else{
      // A healthy human is green
      this.r = 0.0;
      this.g = 1.0;
      this.b = 0.0;
      this.a = 1.0;
    }
  }
}

def initField(field){
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = 0.0;
}


def move() {
  var dx = this.dx;
  var dy = this.dy;

  var newX = this.x + dx;
  var newY = this.y + dy;

  if (newX < 0.0) {
    newX = -newX;
    dx = -dx;
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x);
    dx = -dx;
  }
  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y);
    dy = -dy;
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y);
    dy = -dy;
  }

  this.x = newX;
  this.y = newY;
  this.dx = dx;
  this.dy = dy;
}

def spreadDisease(field) {
  var pathogenConc = field.a;
  
  if (this.LastInfectionTime > 0){
    var spreadFactor = 0.5;
    pathogenConc = pathogenConc + spreadFactor;
  }
  
  if (pathogenConc > 1.0){
    pathogenConc = 1.0;
  }
  
  // Possible bug: One must set these
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = pathogenConc;
}

def decay(field) {
  var decayFactor = 0.0001;
  var pathogenConc = field.a - decayFactor;
  
  if (pathogenConc < 0.0){
    pathogenConc = 0.0;
  }
  
  // Possible bug: One must set these
  field.r = 0.0;
  field.g = 0.0;
  field.b = 1.0;
  field.a = pathogenConc;
}

def infect(field, time){
  if (this.LastInfectionTime < 0){
    // Human is not infected
    if (field.a > this.Immunity){
      // The human becomes infected
      this.LastInfectionTime = time;
      
      // Possible bug: One must these
      this.r = 1.0;
      this.g = 0.0;
      this.b = 0.0;
      this.a = 1.0
    }
  }
}

def heal(time){
  if (this.LastInfectionTime > 0){
    var timeDiff = abs(time - this.LastInfectionTime);
		
    // Heal human after threshold
    var healThreshold = 20;
    if (timeDiff > healThreshold){
      this.LastInfectionTime = -1.0;
      this.Immunity = 1.0;
      
      // Possible bug: One must these
      this.r = 0.0;
      this.g = 0.7;
      this.b = 0.0;
      this.a = 1.0;
    }
  }
}
